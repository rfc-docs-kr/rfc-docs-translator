

```text
Network Working Group                                           G. Klyne
Request for Comments: 3339                        Clearswift Corporation
Category: Standards Track                                      C. Newman
                                                        Sun Microsystems
                                                               July 2002
```

- 인터넷상의 날짜와 시간: 타임스탬프

---
# **Status of this Memo**

이 문서는 인터넷 커뮤니티를 위한 인터넷 표준 추적 프로토콜을 지정하고 개선을 위한 토론 및 제안을 요청합니다. 이 프로토콜의 표준화 상태 및 상태에 대해서는 "Internet Official Protocol Standards"\(STD 1\) 최신판을 참조하십시오. 이 메모의 배포는 무제한입니다.

---
# **Copyright Notice**

저작권\(C\)인터넷학회\(2002\). 판권 소유.

---
# **Abstract**

이 문서는 그레고리력을 사용하여 날짜와 시간을 표현하기 위한 ISO 8601 표준의 프로필인 인터넷 프로토콜에 사용할 날짜 및 시간 형식을 정의합니다.

---
# **Table of Contents**

```text
   1. Introduction ............................................ 2
   2. Definitions ............................................. 3
   3. Two Digit Years ......................................... 4
   4. Local Time .............................................. 4
   4.1. Coordinated Universal Time (UTC) ...................... 4
   4.2. Local Offsets ......................................... 5
   4.3. Unknown Local Offset Convention ....................... 5
   4.4. Unqualified Local Time ................................ 5
   5. Date and Time format .................................... 6
   5.1. Ordering .............................................. 6
   5.2. Human Readability ..................................... 6
   5.3. Rarely Used Options ................................... 7
   5.4. Redundant Information ................................. 7
   5.5. Simplicity ............................................ 7
   5.6. Internet Date/Time Format ............................. 8
   5.7. Restrictions .......................................... 9
   5.8. Examples ............................................. 10
   6. References ............................................. 10
   7. Security Considerations ................................ 11
   Appendix A. ISO 8601 Collected ABNF ....................... 12
   Appendix B. Day of the Week ............................... 14
   Appendix C. Leap Years .................................... 14
   Appendix D. Leap Seconds ..............................,... 15
   Acknowledgements .......................................... 17
   Authors' Addresses ........................................ 17
   Full Copyright Statement .................................. 18
```

---
## **1. Introduction**

날짜 및 시간 형식은 인터넷에서 많은 혼란과 상호 운용성 문제를 야기합니다. 이 문서에서는 발생하는 많은 문제를 해결하고 인터넷 프로토콜에서 날짜와 시간을 표현하고 사용할 때 일관성과 상호 운용성을 향상시키기 위한 권장 사항을 제공합니다.

이 문서에는 그레고리력을 사용하여 날짜와 시간을 표시하기 위한 ISO 8601 \[ISO8601\] 표준의 인터넷 프로필이 포함되어 있습니다.

날짜 및 시간 값이 인터넷 프로토콜에 나타날 수 있는 방법은 여러 가지가 있습니다. 이 문서에서는 일반적인 사용법 중 하나인 즉, 한 가지에만 중점을 둡니다. 인터넷 프로토콜 이벤트에 대한 타임스탬프. 이러한 제한된 고려 사항은 다음과 같은 결과를 가져옵니다.

o 모든 날짜와 시간은 0000AD에서 9999AD 사이의 "현재 시대"로 간주됩니다.

o 표현된 모든 시간은 협정 세계시\(UTC\)와 명시된 관계\(오프셋\)를 갖습니다. \(이것은 현지 시간과 위치를 알 수 있는 일정 관리 애플리케이션의 일부 사용법과 다르지만 UTC와의 실제 관계는 정치인이나 관리자의 알 수 없거나 알 수 없는 행동에 따라 달라질 수 있습니다. UTC 시간은 23일 17:00에 해당합니다. 뉴욕의 2005년 3월은 일광 절약 시간제에 대한 행정 결정에 따라 달라질 수 있습니다. 이 사양에서는 그러한 고려 사항을 명확하게 설명하지 않습니다.\)

o 타임스탬프는 UTC 도입 이전에 발생한 시간을 표현할 수 있습니다. 이러한 타임스탬프는 명시된 시간에 사용 가능한 최선의 방법을 사용하여 표준시를 기준으로 표현됩니다.

o 날짜 및 시간 표현은 순간을 나타냅니다.

- 기간 또는 간격에 대한 설명은 여기서 다루지 않습니다.

---
## **2. Definitions**

이 문서에서 키워드 "MUST", "MUST NOT", "REQUIRED", "SHALL", "SHALL NOT", "SHOULD", "SHOULD NOT", "RECOMMENDED", "MAY" 및 "OPTIONAL"은 다음과 같습니다. RFC 2119\[RFC2119\]에 설명된 대로 해석됩니다.\(MUST NOT\)

- 국제도량형국\(BIPM\)에서 관리하는 UTC 협정 세계시\(UTC\)입니다.

초 시간을 측정하는 기본 단위입니다.

- 국제 단위계. 이는 외부 장에 의해 방해받지 않는 바닥 상태의 세슘-133 원자의 초미세 전이에 의해 흡수되거나 방출되는 마이크로파 빛의 9,192,631,770주기의 지속 시간으로 정의됩니다.

- 분 60초의 기간입니다. 그러나 윤초가 분 단위로 표시되는 방법에 대해서는 섹션 5.7 및 부록 D의 제한 사항도 참조하세요.

- 시간 60분의 시간입니다.

- 일 24시간의 기간입니다.

- 윤년 그레고리력에서 366일로 구성된 1년입니다. 윤년은 4의 정수배로 나누어지는 해입니다. 단, 100주년\(즉, 100의 배수\)인 경우에도 400의 정수배로 나누어집니다.

- ABNF Augmented Backus-Naur Form은 \[ABNF\]에 정의된 대로 프로토콜이나 언어에서 허용되는 문자열을 나타내는 데 사용되는 형식입니다.

이메일 날짜/시간 형식

- RFC 2822 \[IMAIL-UPDATE\]에 정의된 대로 인터넷 메일에서 사용되는 날짜/시간 형식입니다.

인터넷 날짜/시간 형식

- 이 문서의 섹션 5에 정의된 날짜 형식입니다.

- 타임스탬프 이 용어는 이 문서에서 특정 순간의 명확한 표현을 나타내는 데 사용됩니다.

- Z 시간에 적용될 때 00:00의 UTC 오프셋을 나타내는 접미사입니다. 종종 문자 "Z"의 ICAO 표음 알파벳 표현에서 "Zulu"로 사용됩니다.

- 시간 척도에 대한 자세한 내용은 \[NTP\]의 부록 E, \[ISO8601\]의 섹션 3 및 해당 ITU 문서 \[ITU-R-TF\]를 참조하세요.

---
## **3. Two Digit Years**

다음 요구 사항은 두 자리 연도의 모호성 문제를 해결하기 위한 것입니다.

- o 인터넷 프로토콜은 날짜로 4자리 연도를 생성해야 합니다.\(MUST\)

- o 2자리 연도 사용은 더 이상 사용되지 않습니다. 2자리 연도가 수신되면 잘못된 해석으로 인해 프로토콜이나 처리 오류가 발생하지 않는 경우에만 허용되어야 합니다\(예: 로깅 또는 추적 목적으로만 사용되는 경우\).

- o 두 자리 연도를 사용하는 프로그램은 1999년 이후의 연도를 세 자리로 표시할 수 있습니다. 이는 프로그램이 단순히 연도에서 1900을 빼고 자릿수를 확인하지 않는 경우에 발생합니다. 이러한 손상된 소프트웨어로 인해 생성된 날짜를 강력하게 처리하려는 프로그램은 세 자리 연도에 1900을 추가할 수 있습니다.

- o 두 자리 연도를 사용하는 프로그램은 1999년 이후의 연도를 ":0", ":1", ... ":9", ";0", ...으로 나타낼 가능성이 있습니다. 연도에서 1900을 빼고 US-ASCII 문자 0에 10년을 더합니다. 이러한 손상된 소프트웨어로 인해 생성된 날짜를 강력하게 처리하려는 프로그램은 숫자가 아닌 십진수를 감지하고 적절하게 해석해야 합니다.

두 자리 연도의 문제는 인터넷 프로토콜에 사용되는 모든 날짜와 시간이 정규화되어야 하는 이유를 충분히 보여줍니다.\(MUST\)

---
## **4. Local Time**
---
### **4.1. Coordinated Universal Time (UTC)**

현지 시간대의 일광 절약 시간 규칙은 매우 복잡하고 예측할 수 없는 시간에 현지 법률에 따라 변경될 수 있으므로 진정한 상호 운용성은 협정 세계시\(UTC\)를 사용하는 것이 가장 좋습니다. 이 사양은 현지 시간대 규칙을 따르지 않습니다.

---
### **4.2. Local Offsets**

현지 시간과 UTC 사이의 오프셋은 유용한 정보인 경우가 많습니다. 예를 들어, 전자 메일\(RFC2822, \[IMAIL-UPDATE\]\)에서 로컬 오프셋은 프롬프트 응답 확률을 결정하는 데 유용한 경험적 방법을 제공합니다. 알파벳 문자열로 로컬 오프셋에 레이블을 지정하려는 시도는 과거 \[IMAIL\], \[HOST-REQ\]에서 상호 운용성이 좋지 않은 결과를 가져왔습니다. 결과적으로 RFC2822 \[IMAIL-UPDATE\]에서는 숫자 오프셋을 필수로 만들었습니다.

숫자 오프셋은 "현지 시간 - UTC"로 계산됩니다. 따라서 UTC의 해당 시간은 현지 시간에서 오프셋을 빼서 결정할 수 있습니다. 예를 들어 18:50:00-04:00은 22:50:00Z와 같은 시간입니다. \(이 예에서는 오프셋의 절대값을 더하여 처리되는 음수 오프셋을 보여줍니다.\)

- 참고: ISO 8601에 따라 숫자 오프셋은 UTC와 정수분 단위로 다른 시간대만 나타냅니다. 그러나 많은 과거 시간대는 정수가 아닌 분 단위로 UTC와 다릅니다. 이러한 기록 타임스탬프를 정확하게 표시하려면 애플리케이션에서 이를 표시 가능한 시간대로 변환해야 합니다.

---
### **4.3. Unknown Local Offset Convention**

UTC 시간을 알고 있지만 현지 시간에 대한 오프셋을 알 수 없는 경우 오프셋 "-00:00"으로 나타낼 수 있습니다. 이는 UTC가 지정된 시간에 대한 기본 참조 지점임을 의미하는 "Z" 또는 "+00:00" 오프셋과 의미상 다릅니다. RFC2822 \[IMAIL-UPDATE\]는 이메일에 대한 유사한 규칙을 설명합니다.

---
### **4.4. Unqualified Local Time**

현재 인터넷에 연결된 많은 장치는 내부 시계를 현지 시간으로 실행하며 UTC를 인식하지 못합니다. 인터넷에는 사양을 작성할 때 현실을 수용하는 전통이 있지만 상호 운용성을 희생하면서 이를 수행해서는 안 됩니다. 한정되지 않은 현지 시간대의 해석은 지구의 약 23/24에서 실패하므로, 한정되지 않은 현지 시간의 상호 운용성 문제는 인터넷에서 허용되지 않는 것으로 간주됩니다. 현지 시간으로 구성되고 해당 UTC 오프셋을 인식하지 못하며 다른 인터넷 시스템과의 시간 동기화에 의존하는 시스템은 UTC와의 올바른 동기화를 보장하는 메커니즘을 사용해야 합니다. 몇 가지 적합한 메커니즘은 다음과 같습니다.\(MUST\)

o NTP\(Network Time Protocol\)를 사용하여 UTC로 시간을 가져옵니다.

o 동일한 현지 시간대에 있는 다른 호스트를 인터넷 게이트웨이로 사용하십시오. 이 호스트는 다른 호스트로 전송되는 규정되지 않은 현지 시간을 수정해야 합니다.\(MUST\)

o 사용자에게 현지 시간대 및 일광 절약 시간제 설정을 묻는 메시지를 표시합니다.

---
## **5. Date and Time format**

이 섹션에서는 날짜 및 시간 형식의 바람직한 품질에 대해 논의하고 인터넷 프로토콜에 사용하기 위한 ISO 8601 프로필을 정의합니다.

---
### **5.1. Ordering**

날짜 및 시간 구성 요소가 가장 낮은 정밀도에서 가장 정확한 순서로 정렬되면 유용한 속성이 달성됩니다. 날짜와 시간의 시간대가 동일하다고 가정하고\(예: 모두 UTC\) 동일한 문자열\(예: 모두 "Z" 또는 모두 "+00:00"\)로 표현되며 모든 시간은 동일한 숫자를 갖습니다. 소수 초 자릿수로 구성된 경우 날짜 및 시간 문자열은 문자열로 정렬될 수 있으며\(예: C의 strcmp\(\) 함수 사용\) 시간 순서에 따른 시퀀스가 ​​생성됩니다. 선택적 구두점이 있으면 이 특성을 위반하게 됩니다.

---
### **5.2. Human Readability**

인간의 가독성은 인터넷 프로토콜의 중요한 특징임이 입증되었습니다. 사람이 읽을 수 있는 프로토콜은 텔넷이 테스트 클라이언트로 충분하고 프로토콜에 대한 지식을 바탕으로 네트워크 분석기를 수정할 필요가 없기 때문에 디버깅 비용을 크게 줄여줍니다. 반면에 사람의 가독성으로 인해 상호 운용성 문제가 발생하는 경우도 있습니다. 예를 들어, 날짜 형식 "10/11/1996"은 국가마다 다르게 해석되므로 글로벌 교환에 전혀 적합하지 않습니다. 또한 \[IMAIL\]의 날짜 형식은 사람들이 텍스트 문자열이 허용된다고 가정하고 세 글자 약어를 다른 언어로 번역하거나 생성하기 더 쉬운 대체 날짜 형식\(예: C 함수에서 사용하는 형식\)에서 상호 운용성 문제를 초래했습니다. c시간\). 이러한 이유로 사람의 가독성과 상호 운용성 사이에 균형을 맞춰야 합니다.

모든 국가의 관례에 따라 읽을 수 있는 날짜 및 시간 형식은 없기 때문에 인터넷 클라이언트는 날짜를 해당 지역에 적합한 표시 형식으로 변환할 준비를 해야 합니다. 여기에는 UTC를 현지 시간으로 변환하는 것이 포함될 수 있습니다.\(SHOULD\)

---
### **5.3. Rarely Used Options**

거의 사용되지 않는 옵션이 포함된 형식은 상호 운용성 문제를 일으킬 가능성이 높습니다. 거의 사용되지 않는 옵션은 알파나 베타 테스트에서 사용될 가능성이 낮기 때문에 구문 분석에서 버그가 발견될 가능성이 적기 때문입니다. 거의 사용되지 않는 옵션은 가능하면 상호 운용성을 위해 필수로 설정하거나 생략해야 합니다.

아래에 정의된 형식에는 거의 사용되지 않는 옵션인 1초 단위만 포함됩니다. 이는 날짜/시간 스탬프의 엄격한 순서가 필요하거나 일반적이지 않은 정밀도 요구 사항이 있는 응용 프로그램에서만 사용될 것으로 예상됩니다.

---
### **5.4. Redundant Information**

날짜/시간 형식에 중복 정보가 포함되어 있으면 중복 정보가 상관되지 않을 가능성이 있습니다. 예를 들어, 날짜/시간 형식에 요일을 포함하면 요일은 부정확하지만 날짜는 정확하거나 그 반대일 가능성이 있습니다. 날짜로부터 요일을 계산하는 것은 어렵지 않으므로\(부록 B 참조\), 요일은 날짜/시간 형식에 포함되어서는 안 됩니다.

---
### **5.5. Simplicity**

ISO 8601 \[ISO8601\]에 지정된 날짜 및 시간 형식의 전체 세트는 다중 표현과 부분 표현을 제공하려는 시도에서 매우 복잡합니다. 부록 A에는 ISO 8601의 전체 구문을 ABNF로 변환하려는 시도가 포함되어 있습니다. 인터넷 프로토콜은 요구 사항이 다소 다르며 단순성이 중요한 특징임이 입증되었습니다. 또한, 인터넷 프로토콜은 일반적으로 진정한 상호 운용성을 달성하기 위해 완전한 데이터 사양이 필요합니다. 따라서 ISO 8601의 전체 문법은 대부분의 인터넷 프로토콜에 비해 너무 복잡한 것으로 간주됩니다.

다음 섹션에서는 인터넷에서 사용하기 위한 ISO 8601의 프로필을 정의합니다. ISO 8601 확장 형식의 하위 집합입니다. 대부분의 필드와 구두점을 필수로 지정하면 단순성이 달성됩니다.

---
### **5.6. Internet Date/Time Format**

ISO 8601 \[ISO8601\] 날짜의 다음 프로필은 인터넷의 새로운 프로토콜에 사용해야 합니다. 이는 \[ABNF\]에 정의된 구문 설명 표기법을 사용하여 지정됩니다.\(SHOULD\)

```text
   date-fullyear   = 4DIGIT
   date-month      = 2DIGIT  ; 01-12
   date-mday       = 2DIGIT  ; 01-28, 01-29, 01-30, 01-31 based on
                             ; month/year
   time-hour       = 2DIGIT  ; 00-23
   time-minute     = 2DIGIT  ; 00-59
   time-second     = 2DIGIT  ; 00-58, 00-59, 00-60 based on leap second
                             ; rules
   time-secfrac    = "." 1*DIGIT
   time-numoffset  = ("+" / "-") time-hour ":" time-minute
   time-offset     = "Z" / time-numoffset

   partial-time    = time-hour ":" time-minute ":" time-second
                     [time-secfrac]
   full-date       = date-fullyear "-" date-month "-" date-mday
   full-time       = partial-time time-offset

   date-time       = full-date "T" full-time
```

- 참고: \[ABNF\] 및 ISO8601에 따라 이 구문의 "T" 및 "Z" 문자는 각각 소문자 "t" 또는 "z"일 수 있습니다.

- 이 날짜/시간 형식은 대문자 'A'-'Z' 및 'a'-'z'\(예: XML\)를 구별하는 일부 환경이나 컨텍스트에서 사용될 수 있습니다. 이러한 환경에서 이 형식을 사용하는 사양은 날짜/시간 구문에 사용되는 'T' 및 'Z' 문자가 항상 대문자여야 하도록 날짜/시간 구문을 추가로 제한할 수 있습니다. 이 형식을 생성하는 애플리케이션은 대문자를 사용해야 합니다.\(MAY, SHOULD\)

참고: ISO 8601은 "T"로 구분된 날짜와 시간을 정의합니다.

- 이 구문을 사용하는 응용 프로그램은 가독성을 위해 전체 날짜와 전체 시간을 공백 문자로 구분하여 지정하도록 선택할 수 있습니다.

---
### **5.7. Restrictions**

문법 요소 date-mday는 이번 달의 일수를 나타냅니다. 최대값은 다음과 같이 월과 연도에 따라 다릅니다.

```text
      Month Number  Month/Year           Maximum value of date-mday
      ------------  ----------           --------------------------
      01            January              31
      02            February, normal     28
      02            February, leap year  29
      03            March                31
      04            April                30
      05            May                  31
      06            June                 30
      07            July                 31
      08            August               31
      09            September            30
      10            October              31
      11            November             30
      12            December             31
```

부록 C에는 해당 연도가 윤년인지 확인하는 샘플 C 코드가 포함되어 있습니다.

문법 요소 time-second는 윤초가 발생하는 달의 말일에 "60" 값을 가질 수 있습니다. 현재까지: 6월\(XXXX-06-30T23:59:60Z\) 또는 12월\(XXXX-12-31T23: 59:60Z\); 윤초 표는 부록 D를 참조하세요. 윤초를 빼는 것도 가능하며, 이 때 time-second의 최대값은 "58"입니다. 그 외의 경우에는 시간-초의 최대값은 "59"입니다. 또한 "Z" 이외의 시간대에서는 윤초 지점이 시간대 오프셋에 의해 이동됩니다\(따라서 전 세계에서 동일한 순간에 발생함\).

윤초는 먼 미래까지 예측할 수 없습니다. 국제 지구 회전 서비스\(International Earth Rotation Service\)는 몇 주 후에 윤초를 경고하는 공지\[IERS\]를 게시합니다. 애플리케이션은 윤초가 발표될 때까지 삽입된 윤초와 관련된 타임스탬프를 생성해서는 안 됩니다.

ISO 8601에서는 시간을 "24"로 허용하지만 ISO 8601의 이 프로필에서는 혼란을 줄이기 위해 시간에 대해 "00"과 "23" 사이의 값만 허용합니다.

---
### **5.8. Examples**

다음은 인터넷 날짜/시간 형식의 몇 가지 예입니다.

```text
      1985-04-12T23:20:50.52Z
```

이는 UTC 기준으로 1985년 4월 12일 23시 이후 20분 50.52초를 나타냅니다.

```text
      1996-12-19T16:39:57-08:00
```

이는 1996년 12월 19일 16시 이후 39분 57초를 나타내며 UTC\(태평양 표준시\)에서 -08:00의 오프셋을 갖습니다. 이는 UTC 기준 1996-12-20T00:39:57Z와 동일합니다.

```text
      1990-12-31T23:59:60Z
```

이는 1990년 말에 삽입된 윤초를 나타냅니다.

```text
      1990-12-31T15:59:60-08:00
```

이는 태평양 표준시에서 UTC보다 8시간 느린 동일한 윤초를 나타냅니다.

```text
      1937-01-01T12:00:27.87+00:20
```

이는 네덜란드 시간으로 1937년 1월 1일 정오와 동일한 순간을 나타냅니다. 네덜란드 표준시는 1909년 5월 1일부터 1937년 6월 30일까지 법적으로 UTC보다 정확히 19분 32.13초 빨랐습니다. 이 시간대는 HH:MM 형식을 사용하여 정확하게 표현할 수 없으며 이 타임스탬프는 표현 가능한 가장 가까운 UTC 오프셋을 사용합니다.

---
## **6. References**

```text
   [ZELLER]       Zeller, C., "Kalender-Formeln", Acta Mathematica, Vol.
                  9, Nov 1886.
```

\[IMAIL\] Crocker, D., "Arpa 인터넷 문자 메시지 형식에 대한 표준", STD 11, RFC 822, 1982년 8월.

```text
   [IMAIL-UPDATE] Resnick, P., "Internet Message Format", RFC 2822,
                  April 2001.
```

\[ABNF\] Crocker, D. 및 P. Overell, "구문 사양을 위한 보강된 BNF: ABNF", RFC 2234, 1997년 11월.

\[ISO8601\] "데이터 요소 및 교환 형식 - 정보 교환 - 날짜 및 시간 표현", ISO 8601:1988\(E\), 국제 표준화 기구, 1988년 6월.

\[ISO8601:2000\] "데이터 요소 및 교환 형식 - 정보 교환 - 날짜 및 시간 표현", ISO 8601:2000, 국제 표준화 기구, 2000년 12월.

\[호스트-요청\] Braden, R., "인터넷 호스트에 대한 요구 사항 - 응용 프로그램 및 지원", STD 3, RFC 1123, 1989년 10월.

```text
   [IERS]         International Earth Rotation Service Bulletins,
                  <http://hpiers.obspm.fr/eop-
                  pc/products/bulletins.html>.
```

\[NTP\] Mills, D, "네트워크 시간 프로토콜\(버전 3\)

- 사양, 구현 및 분석", RFC 1305, 1992년 3월.

\[ITU-R-TF\] 시간 신호 및 주파수 표준 방출에 대한 국제 전기 통신 연합 권장 사항. <http://www.itu.ch/publications/itu-r/iturtf.htm\>

\[RFC2119\] Bradner, S, "요구 사항 수준을 나타내기 위해 RFC에 사용되는 키워드", BCP 14, RFC 2119, 1997년 3월.

---
## **7. Security Considerations**

사이트의 현지 시간대는 시스템이 모니터링될 가능성이 낮고 보안 검색에 더 취약할 수 있는 시간을 결정하는 데 유용할 수 있으므로 일부 사이트에서는 UTC로만 시간을 내보내려고 할 수 있습니다. 다른 사람들은 이것을 편집증으로 인해 유용한 기능이 상실된 것이라고 생각할 수도 있습니다.

---
# **Appendix A. ISO 8601 Collected ABNF**

이 정보는 ISO 8601의 1988년 버전을 기반으로 합니다. 2000년 개정판에는 일부 변경 사항이 있을 수 있습니다.

ISO 8601은 정의된 날짜 및 시간 형식에 대한 공식적인 문법을 지정하지 않습니다. 다음은 ISO 8601에서 정식 문법을 작성하려는 시도입니다. 이는 정보 제공용일 뿐이며 오류가 포함될 수 있습니다. ISO 8601은 여전히 ​​권위 있는 기준입니다.

ISO 8601의 모호성으로 인해 일부 해석이 필요했습니다. 첫째, ISO 8601은 기본 형식과 확장 형식의 혼합이 허용되는지 명확하지 않습니다. 이 문법은 혼합을 허용합니다. ISO 8601에서는 분과 초가 0인 경우에만 24시간이 허용되는지 여부가 명확하지 않습니다. 이는 모든 상황에서 24시간이 허용된다고 가정합니다. 섹션 5.7의 날짜-월요일에 대한 제한 사항이 적용됩니다. ISO 8601에서는 어떤 상황에서는 "T"가 생략될 수 있다고 명시하고 있습니다. 이 문법에는 모호함을 피하기 위해 "T"가 필요합니다. ISO 8601은 또한 \(섹션 5.3.1.3에서\) 1보다 작은 경우 소수점 이하 자릿수에 "0"을 붙일 것을 요구합니다. ISO 8601의 부록 B.2는 소수 앞에 "0"이 오지 않는 예를 제공합니다. 이 문법은 섹션 5.3.1.3이 정확하고 부록 B.2에 오류가 있다고 가정합니다.

```text
   date-century    = 2DIGIT  ; 00-99
   date-decade     =  DIGIT  ; 0-9
   date-subdecade  =  DIGIT  ; 0-9
   date-year       = date-decade date-subdecade
   date-fullyear   = date-century date-year
   date-month      = 2DIGIT  ; 01-12
   date-wday       =  DIGIT  ; 1-7  ; 1 is Monday, 7 is Sunday
   date-mday       = 2DIGIT  ; 01-28, 01-29, 01-30, 01-31 based on
                             ; month/year
   date-yday       = 3DIGIT  ; 001-365, 001-366 based on year
   date-week       = 2DIGIT  ; 01-52, 01-53 based on year

   datepart-fullyear = [date-century] date-year ["-"]
   datepart-ptyear   = "-" [date-subdecade ["-"]]
   datepart-wkyear   = datepart-ptyear / datepart-fullyear

   dateopt-century   = "-" / date-century
   dateopt-fullyear  = "-" / datepart-fullyear
   dateopt-year      = "-" / (date-year ["-"])
   dateopt-month     = "-" / (date-month ["-"])
   dateopt-week      = "-" / (date-week ["-"])

   datespec-full     = datepart-fullyear date-month ["-"] date-mday
   datespec-year     = date-century / dateopt-century date-year
   datespec-month    = "-" dateopt-year date-month [["-"] date-mday]
   datespec-mday     = "--" dateopt-month date-mday
   datespec-week     = datepart-wkyear "W"
                       (date-week / dateopt-week date-wday)
   datespec-wday     = "---" date-wday
   datespec-yday     = dateopt-fullyear date-yday

   date              = datespec-full / datespec-year
                       / datespec-month /
   datespec-mday / datespec-week / datespec-wday / datespec-yday
```

---
# **Time:**

```text
   time-hour         = 2DIGIT ; 00-24
   time-minute       = 2DIGIT ; 00-59
   time-second       = 2DIGIT ; 00-58, 00-59, 00-60 based on
                              ; leap-second rules
   time-fraction     = ("," / ".") 1*DIGIT
   time-numoffset    = ("+" / "-") time-hour [[":"] time-minute]
   time-zone         = "Z" / time-numoffset

   timeopt-hour      = "-" / (time-hour [":"])
   timeopt-minute    = "-" / (time-minute [":"])

   timespec-hour     = time-hour [[":"] time-minute [[":"] time-second]]
   timespec-minute   = timeopt-hour time-minute [[":"] time-second]
   timespec-second   = "-" timeopt-minute time-second
   timespec-base     = timespec-hour / timespec-minute / timespec-second

   time              = timespec-base [time-fraction] [time-zone]

   iso-date-time     = date "T" time
```

---
# **Durations:**

```text
   dur-second        = 1*DIGIT "S"
   dur-minute        = 1*DIGIT "M" [dur-second]
   dur-hour          = 1*DIGIT "H" [dur-minute]
   dur-time          = "T" (dur-hour / dur-minute / dur-second)
   dur-day           = 1*DIGIT "D"
   dur-week          = 1*DIGIT "W"
   dur-month         = 1*DIGIT "M" [dur-day]
   dur-year          = 1*DIGIT "Y" [dur-month]
   dur-date          = (dur-day / dur-month / dur-year) [dur-time]

   duration          = "P" (dur-date / dur-time / dur-week)
```

---
# **Periods:**

```text
   period-explicit   = iso-date-time "/" iso-date-time
   period-start      = iso-date-time "/" duration
   period-end        = duration "/" iso-date-time

   period            = period-explicit / period-start / period-end
```

---
# **Appendix B. Day of the Week**

다음은 0000-03-01 이후 날짜에 대한 요일을 얻는 데 사용할 수 있는 Zeller's Congruence \[Zeller\]를 대략적으로 기반으로 하는 샘플 C 서브루틴입니다.

```text
   char *day_of_week(int day, int month, int year)
   {
      int cent;
      char *dayofweek[] = {
         "Sunday", "Monday", "Tuesday", "Wednesday",
         "Thursday", "Friday", "Saturday"
      };

      /* adjust months so February is the last one */
      month -= 2;
      if (month < 1) {
         month += 12;
         --year;
      }
      /* split by century */
      cent = year / 100;
      year %= 100;
      return (dayofweek[((26 * month - 2) / 10 + day + year
                        + year / 4 + cent / 4 + 5 * cent) % 7]);
   }
```

---
# **Appendix C. Leap Years**

다음은 해당 연도가 윤년인지 계산하는 샘플 C 서브루틴입니다.

```text
   /* This returns non-zero if year is a leap year.  Must use 4 digit
      year.
    */
   int leap_year(int year)
   {
       return (year % 4 == 0 && (year % 100 != 0 || year % 400 == 0));
   }
```

---
# **Appendix D. Leap Seconds**

윤초에 대한 정보는 <http://tycho.usno.navy.mil/leapsec.html\>에서 찾을 수 있습니다. 특히 다음과 같이 지적합니다.

UTC에 윤초를 도입하기로 한 결정은

- 국제지구회전서비스\(IERS\)의 책임. CCIR 권고사항에 따르면 12월 말과 6월의 기회를 1순위로 두고, 3월 말과 9월의 기회를 2순위로 두고 있습니다.

필요한 경우 윤초 삽입은 UTC 기준으로 하루가 끝날 때 추가 초로 발생하며 YYYY-MM-DDT23:59:60Z 형식의 타임스탬프로 표시됩니다. 윤초는 모든 시간대에서 동시에 발생하므로 시간대 관계는 영향을 받지 않습니다. 윤초 시간의 몇 가지 예는 섹션 5.8을 참조하세요.

다음 표는 미국 해군 천문대에서 관리하는 표에서 발췌한 것입니다. 소스 데이터는 다음 위치에 있습니다.

```text
      <ftp://maia.usno.navy.mil/ser7/tai-utc.dat>
```

이 표에는 윤초 날짜와 해당 윤초 이후의 시간 표준 TAI\(윤초로 조정되지 않음\)와 UTC 간의 차이가 표시됩니다.

```text
   UTC Date  TAI - UTC After Leap Second
   --------  ---------------------------
   1972-06-30     11
   1972-12-31     12
   1973-12-31     13
   1974-12-31     14
   1975-12-31     15
   1976-12-31     16
   1977-12-31     17
   1978-12-31     18
   1979-12-31     19
   1981-06-30     20
   1982-06-30     21
   1983-06-30     22
   1985-06-30     23
   1987-12-31     24
   1989-12-31     25
   1990-12-31     26
   1992-06-30     27
   1993-06-30     28
   1994-06-30     29
   1995-12-31     30
   1997-06-30     31
   1998-12-31     32
```

---
# **Acknowledgements**

다음 사람들은 이 문서의 초기 구현에 유용한 조언을 제공했습니다: Ned Freed, Neal McBurnett, David Keegel, Markus Kuhn, Paul Eggert 및 Robert Elz. IETF Calendaring/Scheduling 워킹 그룹 메일링 리스트 참가자들과 시간대 메일링 리스트 참가자들에게도 감사드립니다.

다음 리뷰어들은 현재 개정판에 유용한 제안을 제공했습니다: Tom Harsch, Markus Kuhn, Pete Resnick, Dan Kohn. Paul Eggert는 윤초 및 시간대 오프셋의 미묘함에 관해 많은 주의 깊은 관찰을 제공했습니다. 다음 사람들은 이전 초안에 대한 수정 및 개선 사항을 언급했습니다: Dr John Stockton, Jutta Degener, Joe Abley 및 Dan Wing.

---
# **Authors' Addresses**

```text
   Chris Newman
   Sun Microsystems
   1050 Lakes Drive, Suite 250
   West Covina, CA 91790 USA

   EMail: chris.newman@sun.com
```

Graham Klyne\(편집자, 본 개정판\) Clearswift Corporation 1310 Waterside Arlington Business Park Theale, Reading RG7 4SA UK

```text
   Phone: +44 11 8903 8903
   Fax:   +44 11 8903 9000
   EMail: GK@ACM.ORG
```

---
# **Full Copyright Statement**

저작권\(C\)인터넷학회\(2002\). 판권 소유.

이 문서와 그 번역본은 복사되어 타인에게 제공될 수 있으며, 이에 대해 설명하거나 설명하거나 구현을 지원하는 파생물은 어떤 종류의 제한 없이 전체적으로 또는 부분적으로 준비, 복사, 출판 및 배포될 수 있습니다. , 단, 위의 저작권 표시와 이 단락은 그러한 모든 사본 및 파생물에 포함됩니다. 그러나 이 문서 자체는 인터넷 표준 개발 목적으로 필요한 경우를 제외하고는 저작권 표시를 제거하거나 Internet Society 또는 기타 인터넷 조직에 대한 참조를 제거하는 등 어떤 식으로든 수정할 수 없습니다. 인터넷 표준 프로세스를 따르거나 영어 이외의 언어로 번역하는 데 필요한 대로 따라야 합니다.

위에서 부여된 제한된 권한은 영구적이며 Internet Society 또는 그 승계자 또는 양수인에 의해 취소되지 않습니다.

이 문서와 여기에 포함된 정보는 "있는 그대로" 제공되며 인터넷 사회 및 인터넷 공학 태스크포스는 여기에 포함된 정보의 사용이 상품성 또는 특정 목적에의 적합성에 대한 권리 또는 묵시적 보증을 침해하는 행위.

---
# **Acknowledgement**

RFC 편집기 기능에 대한 자금은 현재 Internet Society에서 제공하고 있습니다.