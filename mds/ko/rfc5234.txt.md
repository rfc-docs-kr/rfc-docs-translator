

```text
Network Working Group                                    D. Crocker, Ed.
Request for Comments: 5234                   Brandenburg InternetWorking
STD: 68                                                       P. Overell
Obsoletes: 4234                                                THUS plc.
Category: Standards Track                                   January 2008

             Augmented BNF for Syntax Specifications: ABNF
```

---
# **Status of This Memo**

이 문서는 인터넷 커뮤니티를 위한 인터넷 표준 추적 프로토콜을 지정하고 개선을 위한 토론 및 제안을 요청합니다. 이 프로토콜의 표준화 상태 및 상태에 대해서는 "Internet Official Protocol Standards"\(STD 1\) 최신판을 참조하십시오. 이 메모의 배포는 무제한입니다.

---
# **Abstract**

인터넷 기술 사양은 형식적인 구문을 정의해야 하는 경우가 많습니다. 수년에 걸쳐 ABNF\(Augmented BNF\)라고 불리는 BNF\(Backus-Naur Form\)의 수정된 버전이 많은 인터넷 사양에서 인기를 끌었습니다. 현재 사양에는 ABNF가 문서되어 있습니다. 컴팩트함과 단순함, 그리고 합리적인 표현력의 균형을 이루고 있습니다. 표준 BNF와 ABNF의 차이점에는 명명 규칙, 반복, 대안, 순서 독립성 및 값 범위가 포함됩니다. 이 사양은 또한 여러 인터넷 사양에 공통된 유형의 핵심 어휘 분석기에 대한 추가 규칙 정의 및 인코딩을 제공합니다.

---
# **Table of Contents**

```text
   1.  Introduction . . . . . . . . . . . . . . . . . . . . . . . . .  3
   2.  Rule Definition  . . . . . . . . . . . . . . . . . . . . . . .  3
     2.1.  Rule Naming  . . . . . . . . . . . . . . . . . . . . . . .  3
     2.2.  Rule Form  . . . . . . . . . . . . . . . . . . . . . . . .  4
     2.3.  Terminal Values  . . . . . . . . . . . . . . . . . . . . .  4
     2.4.  External Encodings . . . . . . . . . . . . . . . . . . . .  6
   3.  Operators  . . . . . . . . . . . . . . . . . . . . . . . . . .  6
     3.1.  Concatenation:  Rule1 Rule2  . . . . . . . . . . . . . . .  6
     3.2.  Alternatives:  Rule1 / Rule2 . . . . . . . . . . . . . . .  7
     3.3.  Incremental Alternatives: Rule1 =/ Rule2 . . . . . . . . .  7
     3.4.  Value Range Alternatives:  %c##-## . . . . . . . . . . . .  8
     3.5.  Sequence Group:  (Rule1 Rule2) . . . . . . . . . . . . . .  8
     3.6.  Variable Repetition:  *Rule  . . . . . . . . . . . . . . .  9
     3.7.  Specific Repetition:  nRule  . . . . . . . . . . . . . . .  9
     3.8.  Optional Sequence:  [RULE] . . . . . . . . . . . . . . . .  9
     3.9.  Comment:  ; Comment  . . . . . . . . . . . . . . . . . . .  9
     3.10. Operator Precedence  . . . . . . . . . . . . . . . . . . . 10
   4.  ABNF Definition of ABNF  . . . . . . . . . . . . . . . . . . . 10
   5.  Security Considerations  . . . . . . . . . . . . . . . . . . . 12
   6.  References . . . . . . . . . . . . . . . . . . . . . . . . . . 12
     6.1.  Normative References . . . . . . . . . . . . . . . . . . . 12
     6.2.  Informative References . . . . . . . . . . . . . . . . . . 12
   Appendix A.  Acknowledgements  . . . . . . . . . . . . . . . . . . 13
   Appendix B.  Core ABNF of ABNF . . . . . . . . . . . . . . . . . . 13
     B.1.  Core Rules . . . . . . . . . . . . . . . . . . . . . . . . 13
     B.2.  Common Encoding  . . . . . . . . . . . . . . . . . . . . . 15
```

---
## **1.  Introduction**

인터넷 기술 사양은 형식적인 구문을 정의해야 하는 경우가 많으며 작성자가 유용하다고 생각하는 표기법을 자유롭게 사용할 수 있습니다. 수년에 걸쳐 ABNF\(Augmented BNF\)라고 불리는 BNF\(Backus-Naur Form\)의 수정된 버전이 많은 인터넷 사양에서 인기를 끌었습니다. 컴팩트함과 단순함, 그리고 합리적인 표현력의 균형을 이루고 있습니다. Arpanet 초기에는 각 사양에 ABNF에 대한 자체 정의가 포함되어 있었습니다. 여기에는 이메일 사양인 \[RFC733\]과 \[RFC822\]가 포함되었으며, 이는 ABNF를 정의하기 위한 일반적인 인용이 되었습니다. 현재 문서에서는 선택적 참조를 허용하기 위해 해당 정의를 분리합니다. 예상대로 일부 수정 및 개선 사항도 제공됩니다.

표준 BNF와 ABNF의 차이점에는 명명 규칙, 반복, 대안, 순서 독립성 및 값 범위가 포함됩니다. 부록 B에서는 여러 인터넷 사양에 공통적인 유형의 핵심 어휘 분석기에 대한 규칙 정의 및 인코딩을 제공합니다. 이는 편의상 제공되는 것이며 이 문서 본문에 정의된 메타 언어 및 형식적인 상태와는 별개입니다.

---
## **2.  Rule Definition**
---
### **2.1.  Rule Naming**

규칙의 이름은 단순히 이름 자체입니다. 즉, 알파벳 문자로 시작하고 뒤에 알파벳, 숫자 및 하이픈\(대시\)이 조합된 일련의 문자입니다.

```text
   NOTE:
```

- 규칙 이름은 대소문자를 구분합니다.

<rulename\>, <Rulename\>, <RULENAME\> 및 <rUlENamE\> 이름은 모두 동일한 규칙을 나타냅니다.

원래 BNF와 달리 꺾쇠 괄호\("<", "\>"\)가 필요하지 않습니다. 그러나 꺾쇠 괄호가 있으면 규칙 이름의 사용을 쉽게 식별할 수 있을 때마다 규칙 이름 주위에 꺾쇠 괄호를 사용할 수 있습니다. 이는 일반적으로 자유 형식 산문의 규칙 이름 참조로 제한되거나 아래 반복에 대한 논의에 표시된 것처럼 공백으로 구분되지 않은 문자열로 결합되는 부분 규칙을 구별하는 데 사용됩니다.

---
### **2.2.  Rule Form**

규칙은 다음 순서로 정의됩니다.

```text
         name =  elements crlf
```

여기서 <name\>은 규칙의 이름이고 <elements\>는 하나 이상의 규칙 이름 또는 터미널 사양이며 <crlf\>는 줄 끝 표시기\(캐리지 리턴 다음에 줄 바꿈\)입니다. 등호는 이름과 규칙 정의를 구분합니다. 요소는 대체 및 반복과 같이 이 문서에 정의된 다양한 연산자에 따라 결합된 하나 이상의 규칙 이름 및/또는 값 정의의 시퀀스를 형성합니다.

시각적 편의를 위해 규칙 정의는 왼쪽 정렬됩니다. 규칙에 여러 줄이 필요한 경우 연속 줄이 들여쓰기됩니다. 왼쪽 정렬 및 들여쓰기는 ABNF 규칙의 첫 번째 줄을 기준으로 하며 문서의 왼쪽 여백과 일치할 필요는 없습니다.

---
### **2.3.  Terminal Values**

규칙은 문자라고도 불리는 최종 값의 문자열로 해석됩니다. ABNF에서 문자는 음수가 아닌 정수일 뿐입니다. 특정 상황에서는 값을 문자 집합\(예: ASCII\)으로 매핑\(인코딩\)하는 것이 지정됩니다.

터미널은 하나 이상의 숫자 문자로 지정되며 해당 문자의 기본 해석은 명시적으로 표시됩니다. 현재 다음 기반이 정의되어 있습니다.

```text
         b           =  binary

         d           =  decimal

         x           =  hexadecimal

   Hence:

         CR          =  %d13

         CR          =  %x0D
```

캐리지 리턴에 대해 각각 \[US-ASCII\]의 10진수 및 16진수 표현을 지정합니다.

이러한 값의 연결된 문자열은 해당 값 내의 문자 구분을 나타내기 위해 마침표\("."\)를 사용하여 간결하게 지정됩니다. 따라서:

```text
         CRLF        =  %d13.10
```

ABNF는 따옴표로 묶인 리터럴 텍스트 문자열을 직접 지정하는 것을 허용합니다. 따라서:

```text
         command     =  "command string"
```

리터럴 텍스트 문자열은 인쇄 가능한 문자의 연결된 집합으로 해석됩니다.

```text
   NOTE:
```

- ABNF 문자열은 대소문자를 구분하지 않으며 이러한 문자열의 문자 집합은 US-ASCII입니다.

```text
   Hence:

         rulename = "abc"

   and:

         rulename = "aBc"
```

"abc", "Abc", "aBc", "abC", "ABc", "aBC", "AbC" 및 "ABC"와 일치합니다.

- 대소문자를 구분하는 규칙을 지정하려면 문자를 개별적으로 지정하십시오.

예를 들어:

```text
         rulename    =  %d97 %d98 %d99

   or

         rulename    =  %d97.98.99
```

소문자 abc로만 구성된 문자열만 일치합니다.

---
### **2.4.  External Encodings**

터미널 값 문자의 외부 표현은 저장 또는 전송 환경의 제약 조건에 따라 달라집니다. 따라서 동일한 ABNF 기반 문법에는 7비트 US-ASCII 환경에 대한 인코딩, 이진 옥텟 환경에 대한 인코딩, 16비트 유니코드가 사용될 때 여전히 다른 인코딩 등 여러 외부 인코딩이 있을 수 있습니다. 인코딩 세부 사항은 ABNF의 범위를 벗어납니다. 하지만 부록 B에서는 대부분의 인터넷에서 일반적으로 사용되는 7비트 US-ASCII 환경에 대한 정의를 제공합니다.

외부 인코딩을 구문에서 분리함으로써 동일한 구문에 대해 대체 인코딩 환경을 사용할 수 있도록 의도되었습니다.

---
## **3.  Operators**
---
### **3.1.  Concatenation:  Rule1 Rule2**

규칙은 일련의 규칙 이름을 나열하여 간단하고 순서가 지정된 값 문자열\(즉, 연속 문자의 연결\)을 정의할 수 있습니다. 예를 들어:

```text
         foo         =  %x61           ; a

         bar         =  %x62           ; b

         mumble      =  foo bar foo
```

<mumble\> 규칙은 소문자 문자열 "aba"와 일치합니다.

선형 공백: 연결은 ABNF 구문 분석 모델의 핵심입니다. 연속된 문자\(값\)의 문자열은 ABNF에 정의된 규칙에 따라 구문 분석됩니다. 인터넷 사양의 경우 선형 공백\(공백 및 가로 탭\)이 특수 문자 또는 원자 문자열 구분과 같은 주요 구문 주위에 자유롭고 암시적으로 산재되도록 허용한 역사가 있습니다.

```text
   NOTE:
```

ABNF에 대한 이 사양은 암시적을 제공하지 않습니다.

- 선형 공백 사양.

구분 기호 또는 문자열 세그먼트 주위에 선형 공백을 허용하려는 문법은 이를 명시적으로 지정해야 합니다. 더 높은 수준의 규칙에서 다양하게 사용되는 "핵심" 규칙에 이러한 공백을 제공하는 것이 유용한 경우가 많습니다. "핵심" 규칙은 어휘 분석기로 구성되거나 단순히 기본 규칙 세트의 일부일 수 있습니다.

---
### **3.2.  Alternatives:  Rule1 / Rule2**

슬래시\("/"\)로 구분된 요소는 대체 요소입니다. 그러므로,

```text
         foo / bar

   will accept <foo> or <bar>.

   NOTE:
```

- 알파벳 문자를 포함하는 따옴표 붙은 문자열은 대체 문자를 지정하기 위한 특수 형식이며 지정된 순서대로, 그러나 대문자와 소문자를 혼합하여 포함된 문자가 포함된 조합 문자열 집합을 나타내는 비터미널로 해석됩니다.

---
### **3.3.  Incremental Alternatives: Rule1 =/ Rule2**

조각으로 대체 목록을 지정하는 것이 편리한 경우가 있습니다. 즉, 초기 규칙은 하나 이상의 대안과 일치할 수 있으며 이후 규칙 정의는 대안 세트에 추가됩니다. 이는 매개변수 목록에서 자주 발생하는 것과 같이 동일한 상위 규칙 세트에서 파생되는 독립적인 사양에 특히 유용합니다. ABNF는 다음 구성을 통해 이러한 증분 정의를 허용합니다.

```text
         oldrule     =/ additional-alternatives
```

그래서 규칙 세트는

```text
         ruleset     =  alt1 / alt2

         ruleset     =/ alt3

         ruleset     =/ alt4 / alt5
```

지정하는 것과 동일합니다.

```text
         ruleset     =  alt1 / alt2 / alt3 / alt4 / alt5
```

---
### **3.4.  Value Range Alternatives:  %c##-##**

대체 숫자 값의 범위는 대체 값의 범위를 나타내기 위해 대시\("-"\)를 사용하여 간결하게 지정할 수 있습니다. 따라서:

```text
         DIGIT       =  %x30-39
```

다음과 같습니다:

```text
         DIGIT       =  "0" / "1" / "2" / "3" / "4" / "5" / "6" /

                        "7" / "8" / "9"
```

연결된 숫자 값과 숫자 값 범위는 동일한 문자열에 지정할 수 없습니다. 숫자 값은 연결을 위해 점 표기법을 사용하거나 하나의 값 범위를 지정하기 위해 대시 표기법을 사용할 수 있습니다. 따라서 줄 끝 시퀀스 사이에 하나의 인쇄 가능한 문자를 지정하려면 다음과 같이 지정하면 됩니다.

```text
         char-line = %x0D.0A %x20-7E %x0D.0A
```

---
### **3.5.  Sequence Group:  (Rule1 Rule2)**

괄호로 묶인 요소는 단일 요소로 처리되며 내용이 엄격하게 정렬됩니다. 따라서,

```text
         elem (foo / bar) blat
```

\(elem foo blat\) 또는 \(elem bar blat\)과 일치하며

```text
         elem foo / bar blat
```

\(elem foo\) 또는 \(bar blat\)과 일치합니다.

```text
   NOTE:
```

- 대안이 여러 규칙 이름이나 리터럴로 구성된 경우 "기본" 대체를 올바르게 읽는 것에 의존하기보다는 그룹화 표기법을 사용하는 것이 좋습니다.

따라서 다음과 같은 형식을 사용하는 것이 좋습니다.

```text
        (elem foo) / (bar blat)
```

일반 독자의 오해를 방지할 수 있습니다.

순서 그룹 표기법은 산문의 요소 순서를 구분하기 위해 자유 텍스트 내에서도 사용됩니다.

---
### **3.6.  Variable Repetition:  *Rule**

요소 앞의 연산자 "\*"는 반복을 나타냅니다. 전체 형식은 다음과 같습니다.

```text
         <a>*<b>element
```

여기서 <a\> 및 <b\>는 선택적 십진수 값으로, 해당 요소가 최소 <a\>에서 최대 <b\>회 발생함을 나타냅니다.

기본값은 0과 무한대이므로 \*<element\>는 0을 포함한 모든 숫자를 허용합니다. 1\*<element\>에는 적어도 하나가 필요합니다. 3\*3<element\>는 정확히 3을 허용합니다. 1\*2<element\>는 1개 또는 2개를 허용합니다.

---
### **3.7.  Specific Repetition:  nRule**

형식의 규칙:

```text
         <n>element
```

는 다음과 같습니다

```text
         <n>*<n>element
```

즉, <element\>가 정확히 <n\>회 발생합니다. 따라서 2DIGIT는 2자리 숫자이고, 3ALPHA는 세 개의 알파벳 문자로 구성된 문자열입니다.

---
### **3.8.  Optional Sequence:  [RULE]**

대괄호는 선택적 요소 시퀀스를 묶습니다.

```text
         [foo bar]
```

는 다음과 같습니다

```text
         *1(foo bar).
```

---
### **3.9.  Comment:  ; Comment**

세미콜론은 줄 끝까지 계속되는 주석을 시작합니다. 이는 사양과 병행하여 유용한 참고 사항을 포함하는 간단한 방법입니다.

---
### **3.10.  Operator Precedence**

위에 설명된 다양한 메커니즘은 상단의 가장 높은\(가장 단단한 바인딩\)부터 하단의 가장 낮은\(가장 느슨한\) 순으로 다음과 같은 우선순위를 갖습니다.

```text
      Rule name, prose-val, Terminal value

      Comment

      Value range

      Repetition

      Grouping, Optional

      Concatenation

      Alternative
```

연결과 자유롭게 혼합된 대체 연산자를 사용하면 혼란스러울 수 있습니다.

- 다시 말하지만, 명시적인 연결 그룹을 만들려면 그룹화 연산자를 사용하는 것이 좋습니다.

---
## **4.  ABNF Definition of ABNF**

```text
   NOTES:
```

- 1. 이 구문에는 상대적으로 엄격한 규칙 형식이 필요합니다. 따라서 사양에 포함된 규칙 세트 버전은 ABNF 파서에서 해석될 수 있도록 사전 처리가 필요할 수 있습니다.

- 2. 이 구문은 부록 B에 제공된 규칙을 사용합니다.

```text
         rulelist       =  1*( rule / (*c-wsp c-nl) )

         rule           =  rulename defined-as elements c-nl
                                ; continues if next line starts
                                ;  with white space

         rulename       =  ALPHA *(ALPHA / DIGIT / "-")

         defined-as     =  *c-wsp ("=" / "=/") *c-wsp
                                ; basic rules definition and
                                ;  incremental alternatives

         elements       =  alternation *c-wsp

         c-wsp          =  WSP / (c-nl WSP)

         c-nl           =  comment / CRLF
                                ; comment or newline

         comment        =  ";" *(WSP / VCHAR) CRLF

         alternation    =  concatenation
                           *(*c-wsp "/" *c-wsp concatenation)

         concatenation  =  repetition *(1*c-wsp repetition)

         repetition     =  [repeat] element

         repeat         =  1*DIGIT / (*DIGIT "*" *DIGIT)

         element        =  rulename / group / option /
                           char-val / num-val / prose-val

         group          =  "(" *c-wsp alternation *c-wsp ")"

         option         =  "[" *c-wsp alternation *c-wsp "]"

         char-val       =  DQUOTE *(%x20-21 / %x23-7E) DQUOTE
                                ; quoted string of SP and VCHAR
                                ;  without DQUOTE

         num-val        =  "%" (bin-val / dec-val / hex-val)

         bin-val        =  "b" 1*BIT
                           [ 1*("." 1*BIT) / ("-" 1*BIT) ]
                                ; series of concatenated bit values
                                ;  or single ONEOF range

         dec-val        =  "d" 1*DIGIT
                           [ 1*("." 1*DIGIT) / ("-" 1*DIGIT) ]

         hex-val        =  "x" 1*HEXDIG
                           [ 1*("." 1*HEXDIG) / ("-" 1*HEXDIG) ]

         prose-val      =  "<" *(%x20-3D / %x3F-7E) ">"
                                ; bracketed string of SP and VCHAR
                                ;  without angles
                                ; prose description, to be used as
                                ;  last resort
```

---
## **5.  Security Considerations**

보안은 이 문서와 전혀 관련이 없는 것으로 간주됩니다.

---
## **6.  References**
---
### **6.1.  Normative References**

\[US-ASCII\] 미국 국립 표준 협회\(American National Standards Institute\), "코드화된 문자 집합 - 정보 교환을 위한 7비트 미국 표준 코드", ANSI X3.4, 1986.

---
### **6.2.  Informative References**

\[RFC733\] Crocker, D., Vittal, J., Pogran, K. 및 D. Henderson, "ARPA 네트워크 문자 메시지 형식에 대한 표준", RFC 733, 1977년 11월.

\[RFC822\] Crocker, D., "ARPA 인터넷 문자 메시지 형식에 대한 표준", STD 11, RFC 822, 1982년 8월.

---
# **Appendix A.  Acknowledgements**

ABNF의 구문은 원래 RFC 733에 지정되었습니다. SRI International의 Ken L. Harrenstien은 표현을 더 작고 이해하기 쉽게 만드는 Augmented BNF로 BNF를 다시 코딩하는 일을 담당했습니다.

이 최근 프로젝트는 이메일 이외의 사양 작성자가 반복적으로 인용한 RFC 822의 일부, 즉 Augmented BNF에 대한 설명을 추출하려는 단순한 노력으로 시작되었습니다. 워킹그룹은 단순히 기존 텍스트를 맹목적으로 별도의 문서로 변환하는 것이 아니라 기존 사양과 지난 15년 동안 출시된 관련 사양의 단점과 장점을 세심하게 고려하여 이를 추구하기로 결정했습니다. 상승. 이로 인해 프로젝트는 처음 의도했던 것보다 훨씬 더 야심찬 것으로 바뀌었습니다. 흥미롭게도 목록 표기를 제거하는 등의 결정은 놀라웠지만 결과는 원본과 크게 다르지 않습니다.

이 사양의 "분리된" 버전은 DRUMS 작업 그룹의 일부였으며 Jerome Abela, Harald Alvestrand, Robert Elz, Roger Fajman, Aviva Garrett, Tom Harsch, Dan Kohn, Bill McQuillan, Keith Moore, Chris Newman, 피트 레스닉\(Pete Resnick\), 헤닝 슐츠린\(Henning Schulzrinne\).

Julian Reschke는 Draft Standard 버전을 XML 소스 형식으로 변환한 데 대해 특별히 감사드립니다.

---
# **Appendix B.  Core ABNF of ABNF**

이 부록에는 일반적으로 사용되는 몇 가지 기본 규칙이 포함되어 있습니다. 기본 규칙은 대문자입니다. 이러한 규칙은 7비트 ASCII 또는 7비트 ASCII의 상위 집합인 문자 집합으로 인코딩된 ABNF에만 유효합니다.

---
### **B.1.  Core Rules**

SP, HTAB, CRLF, DIGIT, ALPHA 등과 같은 특정 기본 규칙은 대문자로 표시됩니다.

```text
         ALPHA          =  %x41-5A / %x61-7A   ; A-Z / a-z

         BIT            =  "0" / "1"

         CHAR           =  %x01-7F
                                ; any 7-bit US-ASCII character,
                                ;  excluding NUL

         CR             =  %x0D
                                ; carriage return

         CRLF           =  CR LF
                                ; Internet standard newline

         CTL            =  %x00-1F / %x7F
                                ; controls

         DIGIT          =  %x30-39
                                ; 0-9

         DQUOTE         =  %x22
                                ; " (Double Quote)

         HEXDIG         =  DIGIT / "A" / "B" / "C" / "D" / "E" / "F"

         HTAB           =  %x09
                                ; horizontal tab

         LF             =  %x0A
                                ; linefeed
```

LWSP = \*\(WSP / CRLF WSP\)

- ; 이 선형 공백 규칙을 사용합니다. 흰색만 포함하는 줄을 허용합니다. 더 이상 합법적이지 않은 공간; 메일 헤더로 인해 ; 다른 상호 운용성 문제; 컨텍스트. ; 메일을 정의할 때는 사용하지 마십시오. 헤더 및 주의해서 사용하십시오. 다른 맥락.

```text
         OCTET          =  %x00-FF
                                ; 8 bits of data

         SP             =  %x20

         VCHAR          =  %x21-7E
                                ; visible (printing) characters

         WSP            =  SP / HTAB
                                ; white space
```

---
### **B.2.  Common Encoding**

외부적으로 데이터는 "네트워크 가상 ASCII"\(즉, 8비트 필드의 7비트 US-ASCII\)로 표시되며 상위\(8번째\) 비트는 0으로 설정됩니다. 값 문자열은 "네트워크 바이트 순서"로 되어 있으며, 더 높은 값의 바이트가 왼쪽에 표시되고 네트워크를 통해 먼저 전송됩니다.

---
# **Authors' Addresses**

```text
   Dave Crocker (editor)
   Brandenburg InternetWorking
   675 Spruce Dr.
   Sunnyvale, CA  94086
   US

   Phone: +1.408.246.8253
   EMail: dcrocker@bbiw.net

   Paul Overell
   THUS plc.
   1/2 Berkeley Square,
   99 Berkeley Street
   Glasgow  G3 7HR
   UK

   EMail: paul.overell@thus.net
```

---
# **Full Copyright Statement**

저작권 \(C\) IETF 트러스트\(2008\).

이 문서는 BCP 78에 포함된 권리, 라이선스 및 제한 사항의 적용을 받으며 여기에 명시된 경우를 제외하고 작성자는 모든 권리를 보유합니다.

이 문서 및 여기에 포함된 정보는 "있는 그대로" 제공되며 기여자, 기여자가 대표하거나 후원하는 조직\(있는 경우\), 인터넷 협회, IETF 트러스트 및 인터넷 엔지니어링 태스크포스는 모든 내용을 부인합니다. 여기에 포함된 정보의 사용이 특정 목적에 대한 상품성 또는 적합성에 대한 권리 또는 묵시적 보증을 침해하지 않는다는 보증을 포함하되 이에 국한되지 않는 명시적 또는 묵시적 보증.

---
# **Intellectual Property**

IETF는 이 문서에 설명된 기술의 구현 또는 사용과 관련이 있다고 주장할 수 있는 지적 재산권 또는 기타 권리의 유효성 또는 범위 또는 그러한 권리에 따른 라이센스의 범위에 대해 어떠한 입장도 취하지 않습니다. 는 가능하다; 또한 그러한 권리를 식별하기 위해 독립적인 노력을 기울였다는 것을 나타내지 않습니다. RFC 문서의 권리와 관련된 절차에 대한 정보는 BCP 78 및 BCP 79에서 찾을 수 있습니다.

IETF 사무국에 제출된 IPR 공개 사본 및 사용 가능한 라이선스에 대한 보증 또는 이 사양의 구현자 또는 사용자가 이러한 독점권 사용에 대한 일반 라이선스 또는 허가를 얻으려는 시도의 결과를 얻을 수 있습니다. http://www.ietf.org/ipr의 IETF 온라인 IPR 저장소에서.

IETF는 이 표준을 구현하는 데 필요할 수 있는 기술을 포함할 수 있는 저작권, 특허 또는 특허 출원 또는 기타 소유권에 관심을 갖도록 이해 당사자를 초대합니다. 정보를 IETF\(ietf-ipr@ietf.org\)로 보내주십시오.